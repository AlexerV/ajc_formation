000010*******************************************************************
000020*  PROGRAMME PERMET DE MONTRER LA GESTION DES TRANSACTIONS
000040*
000041*******************************************************************
000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. A7CALC3.
000300
000400 ENVIRONMENT DIVISION.
000500 CONFIGURATION SECTION.
000600 SPECIAL-NAMES.
000700     DECIMAL-POINT IS COMMA.
000800
000900 DATA DIVISION.
001000 WORKING-STORAGE SECTION.
001100      COPY DFHBMSCA.
001200      COPY DFHAID.
001300      COPY A7MS3.
001800
001810********************************************************************
001820*  ZONE EST CONSTITUE DE 3 ELEMENTS
001830*  - COMMA-PGM 	: NOM DU PGM
001840*  - INFOS 		: INFORMATIONS A TRANSMETTRE
001863*
001864********************************************************************
001870
001900 01 ZONE.
002000    05 COMMA-PGM     PIC X(8) VALUE 'A7CALC3'.
002100    05 INFOS         PIC X(62).
002310
002391
002395
002397 01 ERR-MESS PIC X(50) VALUE 'FIN'.
002399 77 WS-CD-ERR     PIC 9(2).
002409
002510 77 WS-TEMPS      PIC S9(15) COMP-3.
002520
002530 77 WS-RES        PIC S9(10) VALUE ZERO.
002531
002532 77 ED-RES        PIC +Z(9)9.
002533 77 ED-NUM1       PIC Z(8)9.
002534 77 ED-NUM2       PIC Z(8)9.
002540
002550 LINKAGE SECTION.
002560 01 DFHCOMMAREA PIC X(70).
002570
002600 PROCEDURE DIVISION USING DFHCOMMAREA.
002800********************************************************
002900* EIBCALEN CORRESPOND A LAL LG DES PARAMETRES TRANSMIS
003000* SI EIBCALEN = 0 ==> PREMIERE FOIS
003100********************************************************
003110      EVALUATE EIBTRNID
003120       WHEN 'T7C3'
003200        IF EIBCALEN = ZERO OR EIBAID = DFHPF5
003210                MOVE LOW-VALUE TO A7MAP3O
003310                PERFORM ENVOI-ECRAN
003400        END-IF
003500
003600********************************************************
003700* EIBAID PERMET DE RECUPERER LA TOUCHE APPUYEE
003800* DFHCLEAR ==> ESC
003900********************************************************
004000        IF   EIBAID = DFHCLEAR
004100              MOVE 'BYE !!' TO ERR-MESS
004200              PERFORM FIN-TOTALE
004300        END-IF
004301
004306
004311
004312        IF EIBAID = DFHENTER OR EIBAID = DFHPF3
004313              PERFORM LECT-ECRAN
004314              PERFORM CALCUL
004315
004316
004317             IF EIBAID = DFHPF3
004318                MOVE MESS1O TO ERR-MESS
004319                  EXEC CICS
004320                    SEND FROM (ERR-MESS)
004321                    LENGTH (LENGTH OF ERR-MESS)
004322                    ERASE
004323                  END-EXEC
004324                  EXEC CICS RETURN END-EXEC
004325
004326             END-IF
004327        END-IF
004328        IF EIBAID NOT = DFHENTER AND EIBAID NOT = DFHPF3 AND
004329             EIBAID NOT = DFHPF5 AND EIBAID NOT = DFHCLEAR
004330              INITIALIZE MESS2O
004400              MOVE 'TOUCHE INVALIDE !' TO MESS1O
004500              MOVE DFHBMUNP TO MESS1A
004600              MOVE DFHPINK   TO MESS1C
005470        END-IF
005473       WHEN OTHER
005476            CONTINUE
005477      END-EVALUATE
005478      PERFORM ENVOI-ECRAN
008500      .
008700
010610********************************************************************
010620*                       LISTE DES PARAGRAPHES
010640********************************************************************
010650 ENVOI-ECRAN.
010651        MOVE COMMA-PGM TO PGMNAMEO
010652        MOVE EIBTRNID  TO TRNNAMEO
010653
010654        EXEC CICS ASKTIME ABSTIME (WS-TEMPS) END-EXEC
010655
010656        EXEC CICS
010657           FORMATTIME ABSTIME (WS-TEMPS)
010658           DDMMYY (DATEJO)
010659           DATESEP ('/')
010660           TIME (HEUREO)
010661           TIMESEP (':')
010662        END-EXEC
010663
010664
010665********************************************************************
010666*       ICI ON ENVOIE LES DIFFERNTES MAPS CONSTITUANT LE MAPSET
010667********************************************************************
010670
010700* ENVOI MAP
010710*****************
010800      EXEC CICS
010900         SEND MAP('A7MAP3')
011000              MAPSET('A7MS3')
011600              RESP(WS-CD-ERR)
011610              ERASE
011620              CURSOR
011630              WAIT
011700      END-EXEC
011800      IF WS-CD-ERR  NOT EQUAL  DFHRESP(NORMAL)
011900           MOVE 'ERR SEND' TO ERR-MESS
012000           PERFORM FIN-TOTALE
012100      END-IF
013200
013210****************************************************
013220* REAFFICHE LA TRANSACTION, ELLE PERMET DE POUVOIR
013230* DE NOUVO RECUPERER LES ELEMENTS SAISIS
013240*****************************************************
013250     EXEC CICS
013260          RETURN TRANSID ('T7C3')
013270          COMMAREA (ZONE)
013280          LENGTH (LENGTH OF ZONE)
013290     END-EXEC
013291     .
013310
013320
014110 FIN-TOTALE.
014120     EXEC CICS
014130       SEND FROM (ERR-MESS)
014140       LENGTH (LENGTH OF ERR-MESS)
014150       WAIT
014160       ERASE
014170     END-EXEC
014180     EXEC CICS RETURN END-EXEC.
014190
014191 LECT-ECRAN.
014200
014210      EXEC CICS
014220         RECEIVE  MAP('A7MAP3')
014230              MAPSET('A7MS3')
014240              RESP(WS-CD-ERR)
014280      END-EXEC
014290      IF WS-CD-ERR  NOT EQUAL  DFHRESP(NORMAL)
014291           MOVE 'ERR REC' TO ERR-MESS
014292           PERFORM FIN-TOTALE
014293      END-IF
014300      .
014400
014500 CALCUL.
014510     MOVE ZERO TO WS-RES
014600     IF NUM1I IS NUMERIC AND NUM2I IS NUMERIC
014700        EVALUATE TYPEOPI
014701           WHEN '-'
014702             COMPUTE WS-RES = NUM1I - NUM2I
014703           WHEN '*'
014704             COMPUTE WS-RES = NUM1I * NUM2I
014705           WHEN '/'
014706             IF NUM2I NOT = ZERO
014707                COMPUTE WS-RES = NUM1I / NUM2I
014708             END-IF
014709           WHEN OTHER
014710             MOVE '+' TO TYPEOPO
014711             COMPUTE WS-RES = NUM1I + NUM2I
014712        END-EVALUATE
014713
014714        MOVE NUM1I   TO ED-NUM1
014715        MOVE NUM2I   TO ED-NUM2
014716        MOVE WS-RES  TO ED-RES
014717
014718        MOVE ED-NUM1 TO NUM1O
014719        MOVE ED-NUM2 TO NUM2O
014720
014721        STRING
014722            ED-NUM1 DELIMITED BY SIZE
014723            SPACE   DELIMITED BY SIZE
014724            TYPEOPO DELIMITED BY SIZE
014725            SPACE   DELIMITED BY SIZE
014726            ED-NUM2 DELIMITED BY SIZE
014727            ' = '   DELIMITED BY SIZE
014728            ED-RES  DELIMITED BY SIZE
014729          INTO MESS1O
014730        END-STRING
014740
014900     ELSE
014910          INITIALIZE MESS2O
014920          MOVE 'ERR NUM1 OU NUM2 !' TO MESS1O
014940          MOVE DFHRED    TO MESS1C
015000     END-IF
019400     .
